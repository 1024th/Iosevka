$$include '../../../meta/macros.ptl'

import [mix barmixL linreg clamp fallback] from '../../../support/utils'
import [DesignParameters] from '../../../meta/aesthetics'
import '../../../support/point' as Point

glyph-module

glyph-block Letter-Cyrillic-Nje : begin
	glyph-block-import CommonShapes
	glyph-block-import Common-Derivatives
	glyph-block-import Letter-Cyrillic-Yeri : CyrYeriShape

	define SLAB-ALL    1
	define SLAB-LOWER  2

	define [CyrNjeShape df top slabType] : glyph-proc
		local rightSB : df.width - df.leftSB
		local xlefttop : mix df.leftSB df.rightSB 0.075
		local jut : Jut * 0.72
		local l : df.leftSB + O
		local r : rightSB - O
		local middle : df.width / 2
		set-width df.width
		include : VBarLeft l 0 top MVertStroke
		include : CyrYeriShape top
			left -- (middle - MVertStroke / 2 * HVContrast)
			right -- (r - O)
			stroke -- MVertStroke
			jut -- jut
			slabType -- slabType
		include : HBar (df.leftSB + MVertStroke * 0.1) middle (top / 2)

		define useItalicShape : para.isItalic && slabType === SLAB-LOWER
		if SLAB : begin
			include : tagged 'serifLT' : if useItalicShape
				then : LeftwardTopSerif l top jut
				else : CenterTopSerif (l + MVertStroke / 2 * HVContrast) top jut
			include : tagged 'serifLB' : if useItalicShape
				then : glyph-proc
				else : CenterBottomSerif (l + MVertStroke / 2 * HVContrast) 0 jut

	create-glyph 'cyrl/Nje' 0x40A : glyph-proc
		local df : DivFrame para.diversityM 3
		set-width df.width
		include : df.markSet.capital
		include : CyrNjeShape df CAP SLAB-ALL

	create-glyph 'cyrl/nje' 0x45A : glyph-proc
		local df : DivFrame para.diversityM 3
		set-width df.width
		include : df.markSet.e
		include : CyrNjeShape df XH SLAB-LOWER
