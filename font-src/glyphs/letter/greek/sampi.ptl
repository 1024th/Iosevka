$$include '../../../meta/macros.ptl'

import [mix barmixL linreg clamp fallback] from '../../../support/utils'
import [DesignParameters] from '../../../meta/aesthetics'

glyph-module

glyph-block Letter-Greek-Sampi : begin
	glyph-block-import CommonShapes

	define KSampiDepth 0.45
	define [SampiShape df top bottom fDoSerifs] : glyph-proc
		include : VBarLeft  df.leftSB  top [mix (top - Stroke) bottom KSampiDepth] MVertStroke
		include : VBarRight df.rightSB top [mix (top - Stroke) bottom KSampiDepth] MVertStroke
		include : VBar      df.middle  top  bottom                                 MVertStroke
		include : HBarTop df.leftSB df.rightSB top
		if (fDoSerifs && SLAB) : include : CenterBottomSerif df.middle 0 MidJutSide

	create-glyph 'grek/SampiArchaic' 0x372 : glyph-proc
		local df : DivFrame para.diversityM 3
		set-width df.width
		include : df.markSet.capital
		include : SampiShape df CAP 0 true

	create-glyph 'grek/sampiArchaic' 0x373 : glyph-proc
		local df : DivFrame para.diversityM 3
		set-width df.width
		include : df.markSet.p
		include : SampiShape df XH Descender false
