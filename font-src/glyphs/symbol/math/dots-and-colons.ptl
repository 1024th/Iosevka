$$include '../../../meta/macros.ptl'

import [mix linreg clamp fallback] from '../../../support/utils'
import [DesignParameters] from '../../../meta/aesthetics'
import '../../../support/point' as Point

glyph-module

glyph-block Symbol-Math-Dots-And-Colons : begin
	glyph-block-import CommonShapes
	glyph-block-import Common-Derivatives
	local radius1 PeriodRadius
	local radius : Math.min PeriodRadius (0.5 * [AdviceStroke 3.5] * PeriodSize / Stroke)
	local left : mix 0 Width (1 / 4)
	local right : mix 0 Width (3 / 4)

	define yUpperColon : SymbolMid + XH / 2 - radius
	define yLowerColon : SymbolMid - XH / 2 + radius
	create-glyph 'therefore' 0x2234 : glyph-proc
		include : DotAt Middle yUpperColon radius
		include : DotAt left   yLowerColon radius
		include : DotAt right  yLowerColon radius

	create-glyph 'because' 0x2235 : glyph-proc
		include : DotAt Middle yLowerColon radius
		include : DotAt left   yUpperColon radius
		include : DotAt right  yUpperColon radius

	create-glyph 'mathcolon' 0x2236 : glyph-proc
		include : DotAt Middle yLowerColon radius
		include : DotAt Middle yUpperColon radius

	create-glyph 'coloncolon' 0x2237 : glyph-proc
		include : DotAt left   yUpperColon radius
		include : DotAt right  yUpperColon radius
		include : DotAt left   yLowerColon radius
		include : DotAt right  yLowerColon radius

	create-glyph 'mathcdot' : glyph-proc
		include : DotAt Middle SymbolMid radius

	for-width-kinds WideWidth1
		create-glyph [MangleName 'mathcdotBig'] : glyph-proc
			set-width MosaicWidth
			include : DotAt MosaicMiddle SymbolMid PeriodRadius
