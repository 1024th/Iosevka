import [add-common-feature add-feature add-lookup] from "./table-util"
extern Set

define MarkClasses {
	'above' 'below' 'overlay' 'slash' 'topright' 'bottomright'
	'trailing' 'lf' 'tieAbove' 'tieBelow'
}

export : define [buildMarkMkmk sink glyphList] : begin
	define mark : add-feature sink 'mark'
	define mkmk : add-feature sink 'mkmk'
	add-common-feature sink mark
	add-common-feature sink mkmk

	local markLookupNames {}
	local mkmkLookupNames {}

	foreach markCls [items-of MarkClasses] : begin
		local markLookup : add-lookup sink : createMTLookup glyphList 'gpos_mark_to_base' { markCls }
		mark.lookups.push    markLookup
		markLookupNames.push markLookup

		local mkmkLookup : add-lookup sink : createMTLookup glyphList 'gpos_mark_to_mark' { markCls }
		mkmk.lookups.push    mkmkLookup
		mkmkLookupNames.push mkmkLookup

	foreach markLookup [items-of markLookupNames] : foreach mkmkLookup [items-of mkmkLookupNames]
		sink.lookupDep.push { markLookup mkmkLookup }

define [createBaseInfo sink glyph allowMarkClsSet] : begin
	local res {.}
	local pushed false
	foreach { markCls anchor } [pairs-of glyph.baseAnchors] : if [allowMarkClsSet.has markCls] : begin
		set pushed true
		set res.(markCls) {.x anchor.x .y anchor.y}
	if pushed : set sink.(glyph.name) res
	return pushed

define [createMarkInfo sink glyph allowMarkClsSet] : begin
	local m null
	foreach { markCls anchor } [pairs-of glyph.markAnchors] : if [allowMarkClsSet.has markCls] : begin
		set m {.class markCls .x anchor.x .y anchor.y}
	if m : set sink.(glyph.name) m
	return m

define [createMTLookup glyphList lookupType markClasses] : begin
	local subtable {.marks {.} .bases {.}}
	local allowMarkClsSet : new Set markClasses
	foreach glyph [items-of glyphList] : begin
		local markRecord : createMarkInfo subtable.marks glyph allowMarkClsSet
		if ((lookupType == 'gpos_mark_to_base') != !(!markRecord)) : begin
			createBaseInfo subtable.bases glyph allowMarkClsSet
	return {.type lookupType .subtables {subtable}}
